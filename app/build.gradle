plugins {
    alias(libs.plugins.android.application)
}

def localProperties = new Properties()
file("${rootDir}/local.properties").withInputStream {localProperties.load(it)}

android {
    signingConfigs {
        release {
            storeFile file(localProperties.get("key.path"))
            storePassword localProperties.get("key.pass")
            keyAlias localProperties.get("key.alias")
            keyPassword localProperties.get("key.pass")
        }
    }
    namespace 'ink.snowland.wkuwku'
    compileSdk 35

    defaultConfig {
        applicationId "ink.snowland.wkuwku"
        minSdk 24
        targetSdk 35
        versionCode 3
        versionName "v0.1.2a"

        buildConfigField "String", "BUILD_TIME", "\"" + new Date().format("EEE MMM dd zzz yyyy") + "\""
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    splits {
        abi {
            enable true
            reset()
            include "armeabi-v7a", "arm64-v8a", "x86_64", "x86"
            universalApk false
        }
    }

    buildFeatures {
        viewBinding true
        dataBinding true
        buildConfig true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {
    implementation libs.preference
    def navigation_version = "2.8.9"
    def room_version = "2.7.1"
    def camera_version = "1.3.0"
    implementation(project(":wkuwku"))
    implementation "com.github.bumptech.glide:glide:4.16.0"
    implementation "androidx.navigation:navigation-fragment:$navigation_version"
    implementation "androidx.navigation:navigation-ui:$navigation_version"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-rxjava3:$room_version"
    implementation "io.reactivex.rxjava3:rxandroid:3.0.0"
    implementation "androidx.preference:preference-ktx:1.2.0"
    implementation "com.google.mlkit:barcode-scanning:17.3.0"
    implementation "androidx.camera:camera-core:$camera_version"
    implementation "androidx.camera:camera-camera2:$camera_version"
    implementation "androidx.camera:camera-view:$camera_version"
    implementation "androidx.camera:camera-lifecycle:$camera_version"
    implementation 'org.apache.commons:commons-compress:1.27.1'
//    implementation "androidx.core:core-splashscreen:1.0.0"
//    implementation "com.google.code.gson:gson:2.13.1"
    implementation libs.appcompat
    implementation libs.material
    implementation libs.activity
    implementation libs.constraintlayout
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
}